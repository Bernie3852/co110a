// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution               //111accccccdddjjj
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

    Not(in=instruction[15], out=ni);
    Mux16(a=ALUout, b=instruction, sel=ni, out=MuxToARegister);

    Or(a=ni, b=instruction[5], out=ToA);
    ARegister(in=MuxToARegister, load=ToA, out=Aout, out[0..14]=addressM);
    And(a=instruction[15], b=instruction[4], out=ToD);
    DRegister(in=ALUout, load=ToD,out=Dout);
    And(a=instruction[15], b=instruction[12], out=AMS);
    Mux16(a=Aout, b=inM, sel=AMS,out=MuxToALU);
    ALU(x=Dout, y=MuxToALU, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=ALUout, zr=zr, ng=ng);
    And(a=instruction[15], b=instruction[3], out=writeM);   //Write to M

    Not(in=ng, out=ps);                         //positive
    Not(in=zr, out=nzr);
    And(a=ps, b=nzr, out=psnzr);                //positive not zero
    And(a=instruction[0], b=psnzr, out = S0);   //positive match
    And(a=instruction[1], b=zr, out = S1);      //zero match
    And(a=instruction[2], b=ng, out = S2);      //negative match
    Or(a=S0, b=S1, out=S01);
    Or(a=S01, b=S2, out=ToPC);

    And(a=instruction[15], b=ToPC, out=PCload);
    PC(in=Aout, load=PCload, inc=true, reset=reset, out[0..14]=pc);
}